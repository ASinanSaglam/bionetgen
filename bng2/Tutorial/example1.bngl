# Example file for BNG2 tutorial.
# All text following the occurence off '#' character in a line is ignored.
#   Written by James R. Faeder
#	       Theoretical Biology and Biophysics Group
#	       Los Alamos National Laboratory
#	       faeder@lanl.gov	       
#   10/28/2005
#   revised 6/9/2006

version("2.0.23");

begin parameters
  1 L0   1
  2 R0   1
  3 A0   5
  4 kp1  0.5
  5 km1  0.1
  6 kp2  1.1
  7 km2  0.1
  8 p1  10
  9 d1   5
 10 kpA  1e1
 11 kmA  0.02
end parameters

begin species
  1  L(r)       L0
  2  R(l,d,Y~U) R0
  3  A(SH2)     A0
end species

begin observables
  Molecules R_dim  R(d!+)
  Molecules R_phos R(Y~P!?)
  Molecules A_R    A(SH2!1).R(Y~P!1)
end observables

begin reaction rules
  1  L(r) + R(l,d) <-> L(r!1).R(l!1,d) kp1, km1
  2  R(l!+,d) + R(l!+,d) <-> R(l!+,d!2).R(l!+,d!2) kp2, km2
  3  R(d!+,Y~U) -> R(d!+,Y~P) p1 
  4  R(Y~P) -> R(Y~U) d1
  5  R(Y~P) + A(SH2) <-> R(Y~P!1).A(SH2!1) kpA, kmA
end reaction rules

# Call with no arguments
generate_network();

# Call with a single parameter
#generate_network({max_iter=>2});

# Call with a hash valued parameter
#generate_network({max_stoich=>{A=>1}});

simulate_ode({t_end=>50,n_steps=>20});

# Print concentratons at unevenly spaced times (array-valued parameter)
#simulate_ode({sample_times=>[1,10,100]});
